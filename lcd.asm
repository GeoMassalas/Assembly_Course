;
;	LCD_hello_world.asm
;---------------------------------------------------------
;	Created: 7/25/2019 8:37:06 PM
;	Author : George Massalas
;
;	Simple program that prints on an LCD
;	conneted to portB Hello World!
;---------------------------------------------------------
;	Connections
;	PB0-7 --> D0-7
;	PA0 --> EN
;	PA1 --> R/W
;	PA2 --> R/S
;	GND --> VSS,K
;	3.3V --> VDD, A (through 470 ohm ressistor)
;	V0 -- > Connected to a potentiometer 
;			for contrast adjustment
;---------------------------------------------------------
;	Used 3.3V LCD with SPLC780D control chip by waveshare
;	https://www.waveshare.com/lcd1602-3.3v-blue.htm
;---------------------------------------------------------
;	Delays generated by delay loop calculator
;	at http://www.bretmulvey.com/avrdelay.html
;---------------------------------------------------------


.def	tempVar1 = R17	
.def	tempVar2 = R18
.def	delay1 = R22 
.def	delay2 = R23	
.def	delay3 = R24

start:
		; Stack Pointer Setup
		LDI tempVar1, HIGH(RAMEND)
		OUT SPH, tempVar1
		LDI tempVar1, LOW(RAMEND)
		OUT SPL, tempVar1

		; Port B as output D0-D7
		LDI tempVar1, 0xFF
		OUT DDRB, tempVar1

		; Port A as output 2 R/S - 1 R/W - 0 Enable 
		LDI tempVar1, 0x07	
		OUT DDRA, tempVar1
		LDI tempVar1, 0x00
		OUT PORTA, tempVar1
		OUT PORTB, tempVar1

lcd_init:
		; Set display with 8bit interface
		call delay100ms
		LDI tempVar1, 0b00110000
		OUT PORTB, tempVar1
		call sendCommand
		call delay5ms
		; function set 8bit and select 2 line display 5x8 dot charfont
		LDI tempVar1, 0b00111000
		OUT PORTB, tempVar1
		call sendCommand
		call delay1ms
		; Display On // Cursor Appear // Blinking On
		; D = 1 // C = 1 // B = 1
		LDI tempVar1, 0b00001111
		OUT PORTB, tempVar1
		call sendCommand
		call delay1ms
		; Entry Mode set  // Increment // No display shifting
		; I/D = 1 // S = 0
		LDI tempVar1, 0b00000110
		OUT PORTB, tempVar1
		call sendCommand
		call delay1ms
		; Clear Display
		LDI tempVar1, 0b00000001
		OUT PORTB, tempVar1
		call sendCommand
		call delay5ms
		; Return Home
		LDI tempVar1, 0b00000010
		OUT PORTB, tempVar1
		call sendCommand
		call delay5ms
		
		; Z pointer setup for line 1
		LDI ZH, HIGH(msg1 << 1)
		LDI ZL, LOW(msg1 << 1)
		LDI tempVar2, 0x10	
loop1:
		; Writing to CGRAM Z pointer
		LPM tempVar1, Z+
		OUT PORTB, tempVar1
		LDI tempVar1, 0b00000100
		OUT PORTA, tempVar1
		call sendCommand
		call delay1ms
		DEC tempVar2
		brne loop1

		; Setting Address to line 2 1st letter
		LDI tempVar1, 0x00
		OUT PORTA, tempVar1
		LDI tempVar1, 0b11000000
		OUT PORTB, tempVar1
		call sendCommand
		call delay1ms

		; Z pointer setup for line 2
		LDI ZH, HIGH(msg2 << 1)
		LDI ZL, LOW(msg2 << 1)
		LDI tempVar2, 0x10	
loop2:
		; Writing to CGRAM Z pointer
		LPM tempVar1, Z+
		OUT PORTB, tempVar1
		LDI tempVar1, 0b00000100
		OUT PORTA, tempVar1
		call sendCommand
		call delay1ms
		DEC tempVar2
		brne loop2
loop:
		rjmp loop

; --------------------------------------------------------------
; --------------------- Enable = 1 Pulse -----------------------
; --------------------------------------------------------------
sendCommand:
		cbi PORTA, 0
		call delay1ms
		sbi PORTA, 0
		call delay1ms
		cbi PORTA, 0
		ret

; --------------------------------------------------------------
; ------------------------- 100ms Delay ------------------------
; --------------------------------------------------------------
delay100ms:
		LDI delay1, 6
		LDI delay2, 157
		LDI delay3, 21
dl100:
		DEC  delay3
		brne dl100
		DEC  delay2
		brne dl100
		DEC  delay1
		brne dl100
		RET

; --------------------------------------------------------------
; -------------------------- 5ms Delay -------------------------
; --------------------------------------------------------------

delay5ms:
  		LDI delay1, 72
		LDI delay2, 205
dl5:
		DEC  delay2
		BRNE dl5
		DEC  delay1
		BRNE dl5
		RET

; --------------------------------------------------------------
; -------------------------- 1ms Delay -------------------------
; --------------------------------------------------------------

delay1ms:
  		LDI delay1, 15
		LDI delay2, 91
dl1:
		DEC  delay2
		BRNE dl1
		DEC  delay1
		BRNE dl1
		RET

; --------------------------------------------------------------	
; -------------------------- Messages --------------------------
; --------------------------------------------------------------
; msg1: the 1st line that we want to print on the LCD
; msg2: the 2nd line that we want to print on the LCD
msg1:	
		.DB ' ','H','e','l','l','o',' ','W','o','r','l','d','!','!','!',' '
msg2:	
		.DB 'M','i','c','r','o','c','o','n','t','r','o','l','l','e','r','s'
